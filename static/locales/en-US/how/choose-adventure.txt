Make a text adventure

One fun kind of game is a /choose your own adventure/ game, where a player navigates a series of choices, and tries to find a way to happy ending. 

To make one of these, we can use @Structure, @Key, @Reaction, and @List together to make a collection of decisions, each one with a set of options, like this:

\
¶ This makes a data structure that stores a choice and which decision it will navigate to ¶
•Option(text•'' decision•#)

¶ This data structure stores a list of choices ¶
•Decision(options: [Option])

¶ This is the game content! Change the text in the Options to create atmosphere and tough choices.
Add options to create a web of tricky choices. ¶
decisions: [
		Decision([Option('start' 2)])
		Decision([
				Option('left' 3)
				Option('straight' 4)
				Option('right' 4)
			])
		Decision([Option('win' 1)])
		Decision([Option('lose' 1)])
	]

¶ Convert the key to a number, if it is one ¶
key: Key()
choice: key → #

¶ This value represents the current decision in the list above.
When the key is pressed, we do one of the following.
If there's only one choice for this decision, then we change the current decision
to the option's target decision no matter what key was pressed.
If the key pressed is between 1 and the number of options, then we go to that option's target decision.
Otherwise, we just keep the current decision the same. ¶
current•#: 1 …
	∆ key …
		decisions[current].options.length() = 1 ?
			decisions[current].options[1].decision
			((choice ≥ 1) & (choice ≤ decisions[current].options.length())) ?
				decisions[current].options[choice].decision
				current

¶ This just shows the options as a stack of phrases. You can change this to add instructions, animations, and other spiciness! ¶
Stage([
		Group(
			Stack()
			decisions[current].options.translate(ƒ(option) Phrase(option.text))
		)
	])

\

This pattern looks complex, but all it's really doing is showing the current set of options and each time a key changes, going to the decision of the option chosen. Designing a game is all about changing the content at the top!

offer-choices