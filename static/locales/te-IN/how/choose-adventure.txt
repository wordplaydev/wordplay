టెక్స్ట్ అడ్వెంచర్ చేయండి

ఒక రకమైన సరదా ఆట ఏమిటంటే/మీ స్వంత సాహసం ఎంచుకోండి/ఆట, దీనిలో ఆటగాడు వరుస ఎంపికలను నావిగేట్ చేస్తాడు మరియు సుఖాంతం కోసం ఒక మార్గాన్ని కనుగొనడానికి ప్రయత్నిస్తాడు.

వీటిలో ఒకదాన్ని తయారు చేయడానికి, మనం ఉపయోగించవచ్చు @Structure , @Key , @Reaction , మరియు @List కలిసి నిర్ణయాల సేకరణను తీసుకోండి, ప్రతి ఒక్కటి ఇలాంటి ఎంపికల సమితితో:

\¶ఇది ఒక ఎంపికను నిల్వ చేసే డేటా నిర్మాణాన్ని చేస్తుంది మరియు అది ఏ నిర్ణయానికి నావిగేట్ చేస్తుంది¶
•Option(text•'' decision•#)
¶ఈ డేటా నిర్మాణం ఎంపికల జాబితాను నిల్వ చేస్తుంది¶•Decision(options: [Option])
¶ఇది ఆట కంటెంట్! వాతావరణం మరియు కఠినమైన ఎంపికలను సృష్టించడానికి ఎంపికలలోని వచనాన్ని మార్చండి.గమ్మత్తైన ఎంపికల వెబ్‌ను సృష్టించడానికి ఎంపికలను జోడించండి.¶
decisions: [
		Decision([Option('ప్రారంభం' 2)])
		Decision([
				Option('ఎడమ' 3)
				Option('నేరుగా' 4)
				Option('కుడి' 4)
			])
		Decision([Option('గెలుపు' 1)])
		Decision([Option('ఓడిపో' 1)])
	]
¶అది ఒక సంఖ్య అయితే, కీని ఒక సంఖ్యగా మార్చండి.¶
key: Key()
choice: key → #
¶ఈ విలువ పై జాబితాలోని ప్రస్తుత నిర్ణయాన్ని సూచిస్తుంది.కీని నొక్కినప్పుడు, మనం ఈ క్రింది వాటిలో ఒకదాన్ని చేస్తాము.ఈ నిర్ణయానికి ఒకే ఒక ఎంపిక ఉంటే, మేము ప్రస్తుత నిర్ణయాన్ని మారుస్తాము.ఏ కీ నొక్కినా ఆప్షన్ యొక్క లక్ష్య నిర్ణయానికి.నొక్కిన కీ 1 మరియు ఎంపికల సంఖ్య మధ్య ఉంటే, మనం ఆ ఎంపిక యొక్క లక్ష్య నిర్ణయానికి వెళ్తాము.లేకపోతే, మేము ప్రస్తుత నిర్ణయాన్నే అలాగే ఉంచుతాము.¶
current•#: 1 … ∆ key … decisions[current].options.length() = 1 ? decisions[current].options[1].decision ((choice ≥ 1) & (choice ≤ decisions[current].options.length())) ? decisions[current].options[choice].decision current
¶ఇది ఎంపికలను పదబంధాల స్టాక్‌గా చూపిస్తుంది. మీరు సూచనలు, యానిమేషన్‌లు మరియు ఇతర ఘాటులను జోడించడానికి దీన్ని మార్చవచ్చు!¶Stage([
		Group(Stack() decisions[current].options.translate(ƒ(option) Phrase(option.text)))
	])\

ఈ నమూనా సంక్లిష్టంగా కనిపిస్తుంది, కానీ ఇది నిజంగా చేస్తున్నదంతా ప్రస్తుత ఎంపికల సమితిని చూపించడం మరియు ప్రతిసారీ ఒక కీ మారినప్పుడు, ఎంచుకున్న ఎంపిక నిర్ణయానికి వెళ్లడం. గేమ్‌ను డిజైన్ చేయడం అంటే పైన ఉన్న కంటెంట్‌ను మార్చడం గురించి!

offer-choices