Δημιουργήστε μια περιπέτεια κειμένου

Ένα διασκεδαστικό είδος παιχνιδιού είναι/διάλεξε τη δική σου περιπέτεια/παιχνίδι, όπου ένας παίκτης πλοηγείται σε μια σειρά από επιλογές και προσπαθεί να βρει έναν τρόπο να καταλήξει σε ευτυχές τέλος.

Για να φτιάξουμε ένα από αυτά, μπορούμε να χρησιμοποιήσουμε @Structure , @Key , @Reaction , και @List μαζί για να λάβουν μια συλλογή αποφάσεων, καθεμία με ένα σύνολο επιλογών, όπως αυτή:

\¶Αυτό δημιουργεί μια δομή δεδομένων που αποθηκεύει μια επιλογή και την απόφαση στην οποία θα οδηγήσει.¶
•Option(text•'' decision•#)
¶Αυτή η δομή δεδομένων αποθηκεύει μια λίστα επιλογών¶•Decision(options: [Option])
¶Αυτό είναι το περιεχόμενο του παιχνιδιού! Αλλάξτε το κείμενο στις Επιλογές για να δημιουργήσετε ατμόσφαιρα και δύσκολες επιλογές.Προσθέστε επιλογές για να δημιουργήσετε ένα πλέγμα από δύσκολες επιλογές.¶
decisions: [
		Decision([Option('αρχή' 2)])
		Decision([
				Option('αριστερά' 3)
				Option('ευθεία' 4)
				Option('δικαίωμα' 4)
			])
		Decision([Option('νίκη' 1)])
		Decision([Option('χάνω' 1)])
	]
¶Μετατρέψτε το κλειδί σε αριθμό, αν είναι ένα¶
key: Key()
choice: key → #
¶Αυτή η τιμή αντιπροσωπεύει την τρέχουσα απόφαση στην παραπάνω λίστα.Όταν πατάμε το πλήκτρο, κάνουμε ένα από τα εξής.Εάν υπάρχει μόνο μία επιλογή για αυτήν την απόφαση, τότε αλλάζουμε την τρέχουσα απόφασηστην απόφαση στόχου της επιλογής, ανεξάρτητα από το πλήκτρο που πατήθηκε.Εάν το πατημένο πλήκτρο είναι μεταξύ 1 και του αριθμού των επιλογών, τότε μεταβαίνουμε στην απόφαση-στόχο αυτής της επιλογής.Διαφορετικά, απλώς διατηρούμε την τρέχουσα απόφαση ως έχει.¶
current•#: 1 … ∆ key … decisions[current].options.length() = 1 ? decisions[current].options[1].decision ((choice ≥ 1) & (choice ≤ decisions[current].options.length())) ? decisions[current].options[choice].decision current
¶Αυτό εμφανίζει απλώς τις επιλογές ως μια στοίβα φράσεων. Μπορείτε να το αλλάξετε αυτό για να προσθέσετε οδηγίες, κινούμενα σχέδια και άλλες πινελιές!¶Stage([
		Group(Stack() decisions[current].options.translate(ƒ(option) Phrase(option.text)))
	])\

Αυτό το μοτίβο φαίνεται περίπλοκο, αλλά στην πραγματικότητα το μόνο που κάνει είναι να δείχνει το τρέχον σύνολο επιλογών και κάθε φορά που αλλάζει ένα κλειδί, καταλήγει στην απόφαση για την επιλογή που έχει επιλεγεί. Ο σχεδιασμός ενός παιχνιδιού έχει να κάνει με την αλλαγή του περιεχομένου στην κορυφή!

offer-choices