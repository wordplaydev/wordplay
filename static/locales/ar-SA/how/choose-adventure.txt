اصنع لعبة مغامرات نصية

أحد أنواع الألعاب الممتعة هو/اختر مغامرتك الخاصة/لعبة يتنقل فيها اللاعب بين سلسلة من الخيارات، ويحاول إيجاد طريق إلى نهاية سعيدة.

لصنع واحدة من هذه، يمكننا استخدام @Structure ، @Key ، @Reaction ، و  @List يجتمعون معًا لاتخاذ مجموعة من القرارات، كل منها يتضمن مجموعة من الخيارات، على النحو التالي:

\¶وهذا يُنشئ بنية بيانات تخزن خيارًا والقرار الذي سينتقل إليه¶
•Option(text•'' decision•#)
¶يخزن هيكل البيانات هذا قائمة بالخيارات¶•Decision(options: [Option])
¶هذا هو محتوى اللعبة! غيّر النص في الخيارات لخلق جوٍّ مميز وخيارات صعبة.أضف خيارات لإنشاء شبكة من الخيارات المعقدة.¶
decisions: [
		Decision([Option('يبدأ' 2)])
		Decision([
				Option('غادر' 3)
				Option('مستقيم' 4)
				Option('يمين' 4)
			])
		Decision([Option('يفوز' 1)])
		Decision([Option('يخسر' 1)])
	]
¶حوّل المفتاح إلى رقم، إذا كان رقمًا واحدًا¶
key: Key()
choice: key → #
¶تمثل هذه القيمة القرار الحالي في القائمة أعلاه.عند الضغط على المفتاح، نقوم بأحد الإجراءات التالية.إذا لم يكن هناك سوى خيار واحد لهذا القرار، فإننا نغير القرار الحالي.إلى قرار الهدف الخاص بالخيار بغض النظر عن المفتاح الذي تم الضغط عليه.إذا كان الرقم الذي تم الضغط عليه بين 1 وعدد الخيارات، فسننتقل إلى القرار المستهدف لهذا الخيار.وإلا، سنبقي القرار الحالي كما هو.¶
current•#: 1 … ∆ key … decisions[current].options.length() = 1 ? decisions[current].options[1].decision ((choice ≥ 1) & (choice ≤ decisions[current].options.length())) ? decisions[current].options[choice].decision current
¶هذا يعرض الخيارات على شكل مجموعة من العبارات. يمكنك تغيير ذلك لإضافة تعليمات ورسوم متحركة وعناصر أخرى مميزة!¶Stage([
		Group(Stack() decisions[current].options.translate(ƒ(option) Phrase(option.text)))
	])\

يبدو هذا النمط معقدًا، لكنه في الحقيقة لا يفعل سوى عرض مجموعة الخيارات الحالية، وفي كل مرة يتغير فيها مفتاح، ينتقل إلى قرار الخيار المُختار. تصميم اللعبة يدور حول تغيير المحتوى في الأعلى!

offer-choices