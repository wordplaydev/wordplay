=== Adventure/en
•State(action•"" description•[""] options•[State])

stand: State('stand up' ["You can't stand, you're tied down."] [])
cut: State('cut yourself free' [ "You've cut yourself free."] [])
throw: State('throw' [ "You threw the knife"] [stand])
pickup: State('pickup' ['You have a knife.'] [ cut throw ])
roll: State('roll over' ['There is a knife next to you.'] [ pickup stand ])
start: State("start" ["You awaken in a dark, cold room that smells of mold."] [ roll stand ])

key: Key()
state•State:  start … ∆ key … state.options[key → #]

options: state.options.length() > 0 ? 
    state.options.translate(ƒ(option•State) Phrase("***\option.action\***" size: 2m name: option.action duration: 1s entering: Pose(opacity: 0%)))
    [ Phrase("**GAME OVER**") ]


Stage(
  [
		Group(
			Stack()
			[Phrase(state.description duration: 0.5s name: state.action entering: Pose(offset: Place(0m 2m)))].append(options)
      )
  ] 
  background: Color(0 0 0%°) 
  color: Color(100% 0 0°)
  font: "Permanent Marker"
)