Move
=== start
•Cat(position•#m direction•#)

key: Key()
speed: 1m

cat•Cat:  Cat(5m 1) … ∆ key …
    key = "ArrowLeft" ? Cat(cat.position - speed 1) 
    	key = "ArrowRight" ? Cat(cat.position + speed -1) 
    		cat

ƒ close(ball•Place|ø x•#m) 
    ball•ø ? ⊥ 
    (ball.y < 1m) &
    (
        ((ball.x - x).positive() < 1m) | 
        ((ball.x - (x + 2m)).positive() < 1m)
    )

position: Motion(
    Place(0m 5m)
    nextvelocity: 
        close(position cat.position) ? 
            Velocity((Random(1 5) · -1m/s · cat.direction) (Random(2 10) · 1m/s) (cat.direction · 120°/s)) 
            ø
)

ball: Phrase("o" place: position matter: Matter())

meow: Phrase(
    "🐈"
    face: "Noto Color Emoji"
    name: "kitty"
    size: 2m
    place: Place(cat.position 0m)
    resting: Pose(flipx: cat.direction < 0)
    moving: Sequence(sway(5°) 0.25s "zippy")
)

Stage([ball meow] place: Place(0m 0m -10m))