Move
=== start
•Cat(position•#m direction•#)

key: Key()
speed: 1m

cat•Cat:  Cat(5m 1) … ∆ key …
    key = "ArrowLeft" ? Cat(cat.position - speed 1) 
    	key = "ArrowRight" ? Cat(cat.position + speed -1) 
    		cat

ƒ close(ball•Place|ø x•#m) 
    ball•ø ? ⊥ 
    (ball.y < 1m) &
    (
        ((ball.x - x).positive() < 1m) | 
        ((ball.x - (x + 2m)).positive() < 1m)
    )

colliding: close(ball.place cat.position)

ball: Phrase(
    "o" 
    place: Motion(
        ◆ ? Place(0m 0m) ø
        vx: ◆ ? 0m/s
            colliding ? (Random(1 5) · -1m/s · cat.direction) ø
        vy: ◆ ? 0m/s 
            colliding ? (Random(2 10) · 1m/s) ø
        vrotation: ◆ ? 0°/s 
            colliding ? (cat.direction · 120°/s) ø
    )
)

cat: Phrase(
    "🐈"
    face: "Noto Color Emoji"
    name: "kitty"
    size: 2m
    place: Place(cat.position 0m)
    resting: Pose(flipx: cat.direction < 0)
    moving: Sequence(sway(5°) 0.25s "zippy")
)

Stage([ball cat] place: Place(0m 0m -5m))